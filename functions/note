pbnote () {
	local flag_help flag_input flag_nonewline flag_nopaste
	local arg_title
	local usage=(
		"Usage:"
		"  pbnote                       save clipboard to default note"
		"  pbnote <note name>           save clipboard to named note"
		"Options:"
		"  -h --help                    show this message"
		"  -i --input                   prompt for description"
		"  -n --no-newline              do not add empty line"
		"  -t --title <title>           heading title"
		"  -x --no-paste                do not insert clipboard"
	)

	zmodload zsh/zutil
	zparseopts -D -F -- \
		{h,-help}=flag_help \
		{i,-input}=flag_input \
		{n,-no-newline}=flag_nonewline \
		{t,-title}:=arg_title \
		{x,-no-paste}=flag_nopaste \
		|| return 1
	[[ -z "$flag_help" ]] || { print -l $usage && return }

	local note_home=${NOTE_HOME:-~/notes}
	local note_name=(${*:-${NOTE_NAME:-'pasted-'$(date "+%Y-%m-%d")}})
	local separator=${NOTE_SEPARATOR:-'\n'}

	local pasted
	[[ -z "$flag_nopaste" ]] && pasted=$(pbpaste | col -b)

	local title=${arg_title[-1]}

	local input
	if [[ -n "$flag_input" ]]; then
		print -- "title: ${title}"
		print 'Enter description (^d=exit, ^c=cancel):'
		input=$(cat -)
	fi


	local note_format
	[[ -n "$NOTE_FORMAT" ]] && note_format=($NOTE_FORMAT) \
	  || note_format=(
	    '[$(date)] ${title}'
	    '${input}'
	    '${pasted}'
	    )

	# insert separator if needed
	if [[ -a "${note_home}/${note_name}" ]]; then
		if [[ -z "${flag_nonewline}" ]]; then
			note_format[1]=${separator}${note_format[1]}
		fi
	fi

	# assemble lines to be inserted
	local output
	local item_sep='' # no separator before 1st line
	for item in $note_format
	do
		line=$(eval "print -- \"${item}\"")
		[[ -n "$line" ]] \
			&& output="${output}"${item_sep}$(eval "print -- \"${line}\"")
		item_sep='\n'
	done

   print $output >> ${note_home}/${note_name}

}


note () {
	local flag_help
	local flag_edit
	local usage=(
		"Usage:"
		"  note                         list notes"
		"  note <name>                  show note"
		"Options:"
		"  -e --edit <name>             edit note"
		"  -h --help                    show this message"
	)
	zmodload zsh/zutil
	zparseopts -D -F -- \
		{h,-help}=flag_help \
		{e,-edit}=flag_edit \
		|| return 1
	[[ -z "$flag_help" ]] || { print -l $usage && return }

	local note_home=${NOTE_HOME:-~/notes}
	local editor=(${VISUAL:-${EDITOR:-vi}})

	if [[ -n $* ]]; then
		(
			cd ${note_home}
			if [[ -n "$flag_edit" ]]; then
				eval ${editor} $*
			else
				less -FX $*
			fi
		)
	else
		ls ${note_home}
	fi
}

alias n='note'
